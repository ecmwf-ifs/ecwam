! (C) Copyright 1989- ECMWF.
! 
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.
!

SUBROUTINE WAMINTGR (CDTPRA, CDATE, CDATEWH, CDTIMP, CDTIMPNEXT,  &
 &                   BLK2GLO,                                     &
 &                   WVENVI, WVPRPT, FF_NOW, FF_NEXT, INTFLDS,    &
 &                   WAM2NEMO, MIJ, FL1, XLLWS, TIME1)


! ----------------------------------------------------------------------

!**** *WAMINTGR* - 3-G WAM MODEL - TIME INTEGRATION OF WAVE FIELDS.

!*    PURPOSE.
!     --------

!       COMPUTATION OF THE 2-D FREQUENCY-DIRECTION WAVE SPECTRUM AT ALL
!       GRID POINTS FOR A GIVEN INITIAL SPECTRUM AND FORCING SURFACE
!       STRESS FIELD.

!     REFERENCE.
!     ----------

!         IFS DOCUMENTATION, part VII 

! -------------------------------------------------------------------

USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU
USE YOWDRVTYPE  , ONLY : WVGRIDGLO, ENVIRONMENT, FREQUENCY, FORCING_FIELDS,  &
 &                       INTGT_PARAM_FIELDS, WAVE2OCEAN

USE YOWCOUP  , ONLY : LWNEMOCOU, NEMONTAU
USE YOWGRID  , ONLY : NPROMA_WAM, NCHNK
USE YOWPARAM , ONLY : NIBLO, NANG, NFRE
USE YOWPCONS , ONLY : EPSMIN
USE YOWSTAT  , ONLY : CDTPRO, IDELPRO, IDELT, IDELWI, LLSOURCE
USE YOWWIND  , ONLY : CDAWIFL, CDATEWO, CDATEFL
USE YOWFIELD_MOD, ONLY : FREQUENCY_FIELD, ENVIRONMENT_FIELD, FORCING_FIELDS_FIELD, &
 &  WAVE2OCEAN_FIELD, INTGT_PARAM_FIELDS_FIELD, SOURCE_CONTRIBS_FIELD

USE YOMHOOK  , ONLY : LHOOK, DR_HOOK, JPHOOK

!.....Adding global variables to driver symbol table for offload instructions
USE YOWINDN, ONLY: dal2, k11w, inlcoef, k21w, fklam, fklam1, mfrstlw, ikp1, kfrh, af11, ikm1, dal1, mlsthg, k1w, ikm, fklap,  &
& fklap1, k2w, ikp, rnlcoef
USE YOWCOUP, ONLY: lwnemotauoc, lwnemocoustrn, lwnemocoustk, lwcou, wtauhf, x0tauhf, llgcbz0, lwnemocousend, lwvflx_snl,  &
& lwflux, llnormagam, llcapchnk
USE YOWPHYS, ONLY: satweights, cdisvis, tauwshelter, rnum, chnkmin_u, z0rat, dthrn_u, nsdsnth, z0tubmax, alpha, alphapmax,  &
& alphamin, bmaxokap, gamnconst, indicessat, delta_sdis, swellf5, ndikcumul, cdis, ang_gc_c, cumulw, ang_gc_a, tailfactor,  &
& ang_gc_b, dthrn_a, rnu, betamaxoxkappa2, zalp, tailfactor_pm, rn1_rn
USE YOWPCONS, ONLY: zpi4gm2, zpi4gm1, gm1, sqrtgosurft, zpi, g
USE YOWFRED, ONLY: omxkm3_gc, zpifr, dfim_sim, sinth, delkcc_gc_ns, dfimfr2, flogsprdm1, costh, xkm_gc, c2osqrtvg_gc, xk_gc,  &
& xkmsqrtvgoc2_gc, delkcc_omxkm3_gc, fr5, fr, om3gmkm_gc, cofrm4, omega_gc, rhowg_dfim, cm_gc, flmax, th, nwav_gc, dfim,  &
& delth, dfimofr, dfimfr
USE YOWPARAM, ONLY: nfre_odd, llunstr, nfre_red
USE YOWICE, ONLY: lmaskice, lwamrsetci, ciblock, cdicwa, cithrsh, licerun, cithrsh_tail, lciwabr
USE YOWWNDG, ONLY: icode, icode_cpl
USE YOWSTAT, ONLY: lbiwbk, idamping, isnonlin, iphys
USE YOWALTAS, ONLY: bfcrv, egrcrv, afcrv
USE YOWCOUT, ONLY: lwfluxout
USE YOWWIND, ONLY: wspmin
USE YOWTABL, ONLY: swellft
      
! ----------------------------------------------------------------------

IMPLICIT NONE

#ifdef WAM_PHYS_GPU
   USE IMPLSCH_SCC_MOD, ONLY: implsch_SCC
#else
   #include "implsch.intfb.h"
#endif
#include "incdate.intfb.h"
#include "newwind.intfb.h"
#include "propag_wam.intfb.h"
#include "wam_user_clock.intfb.h"

CHARACTER(LEN=14), INTENT(IN)                                            :: CDTPRA  ! DATE FOR CALL PROPAGATION
CHARACTER(LEN=14), INTENT(INOUT)                                         :: CDATE  ! CURRENT DATE
CHARACTER(LEN=14), INTENT(INOUT)                                         :: CDATEWH ! DATE OF THE NEXT FORCING FIELDS
CHARACTER(LEN=14), INTENT(INOUT)                                         :: CDTIMP  ! START DATE OF SOURCE FUNCTION INTEGRATION
CHARACTER(LEN=14), INTENT(INOUT)                                         :: CDTIMPNEXT  ! NEXT START DATE OF SOURCE FUNCTION INTEGRATION
TYPE(WVGRIDGLO), INTENT(IN)                                              :: BLK2GLO  ! BLOCK TO GRID TRANSFORMATION
TYPE(ENVIRONMENT), INTENT(INOUT)                                         :: WVENVI !  WAVE ENVIRONMENT FIELDS
TYPE(FREQUENCY), INTENT(INOUT)                                           :: WVPRPT  ! WAVE PROPERTIES FIELDS
TYPE(FORCING_FIELDS), INTENT(INOUT)                                      :: FF_NOW  ! FORCING FIELDS AT CURRENT TIME
TYPE(FORCING_FIELDS), INTENT(IN)                                         :: FF_NEXT  !  DATA STRUCTURE WITH THE NEXT FORCING FIELDS
TYPE(INTGT_PARAM_FIELDS), INTENT(INOUT)                                  :: INTFLDS  ! INTEGRATED/DERIVED PARAMETERS
TYPE(WAVE2OCEAN), INTENT(INOUT)                                          :: WAM2NEMO  ! WAVE FIELDS PASSED TO NEMO
INTEGER(KIND=JWIM), DIMENSION(NPROMA_WAM, NCHNK), INTENT(INOUT)          :: MIJ  ! LAST FREQUENCY INDEX OF THE PROGNOSTIC RANGE
REAL(KIND=JWRB), DIMENSION(NPROMA_WAM, NANG, NFRE, NCHNK), INTENT(INOUT) :: FL1
REAL(KIND=JWRB), DIMENSION(NPROMA_WAM, NANG, NFRE, NCHNK), INTENT(INOUT) :: XLLWS ! TOTAL WINDSEA MASK FROM INPUT SOURCE TERM

REAL(KIND=JWRB), INTENT(INOUT) :: TIME1(2)
REAL(KIND=JWRB) :: TIME0


INTEGER(KIND=JWIM) :: IJ, K, M
INTEGER(KIND=JWIM) :: ICHNK
INTEGER(KIND=JWIM) :: IDELWH

! Objects to store fields
TYPE(FREQUENCY_FIELD) :: WVPRPT_FIELD
TYPE(ENVIRONMENT_FIELD) :: WVENVI_FIELD
TYPE(FORCING_FIELDS_FIELD) :: FF_NOW_FIELD
TYPE(WAVE2OCEAN_FIELD) :: WAM2NEMO_FIELD
TYPE(INTGT_PARAM_FIELDS_FIELD) :: INTFLDS_FIELD
TYPE(SOURCE_CONTRIBS_FIELD) :: SRC_CONTRIBS

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE


LOGICAL, SAVE :: LLNEWFILE

DATA LLNEWFILE / .FALSE. /

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('WAMINTGR',0,ZHOOK_HANDLE)

!*     PROPAGATION TIME
!      ----------------

IF (CDATE == CDTPRA) THEN
  TIME0=-WAM_USER_CLOCK()
  CALL PROPAG_WAM(BLK2GLO, WVENVI, WVPRPT, FL1)
  TIME1(1) = TIME1(1) + (TIME0+WAM_USER_CLOCK())*1.E-06
  CDATE = CDTPRO
ENDIF


!* RETRIEVING NEW FORCING FIELDS IF NEEDED.
!  ----------------------------------------
CALL NEWWIND(CDTIMP, CDATEWH, LLNEWFILE,      &
 &           WVPRPT, FF_NOW, FF_NEXT)

! IT IS TIME TO INTEGRATE THE SOURCE TERMS
! ----------------------------------------
IF (CDATE >= CDTIMPNEXT) THEN
! COMPUTE UPDATE DUE TO SOURCE TERMS
  CALL GSTATS(1431,0)
  IF (LLSOURCE) THEN

    TIME0=-WAM_USER_CLOCK()
    CALL WVPRPT_FIELD%INIT(WAVNUM=WVPRPT%WAVNUM, CGROUP=WVPRPT%CGROUP, CIWA=WVPRPT%CIWA, CINV=WVPRPT%CINV, XK2CG=WVPRPT%XK2CG, &
 &                         STOKFAC=WVPRPT%STOKFAC)
    CALL WVENVI_FIELD%INIT(EMAXDPT=WVENVI%EMAXDPT, INDEP=WVENVI%INDEP, DEPTH=WVENVI%DEPTH, IOBND=WVENVI%IOBND, IODP=WVENVI%IODP)
    CALL FF_NOW_FIELD%INIT(AIRD=FF_NOW%AIRD, WDWAVE=FF_NOW%WDWAVE, CICOVER=FF_NOW%CICOVER, WSWAVE=FF_NOW%WSWAVE, &
&                          WSTAR=FF_NOW%WSTAR, UFRIC=FF_NOW%UFRIC, TAUW=FF_NOW%TAUW, TAUWDIR=FF_NOW%TAUWDIR, &
&                          Z0M=FF_NOW%Z0M, Z0B=FF_NOW%Z0B, CHRNCK=FF_NOW%CHRNCK, CITHICK=FF_NOW%CITHICK)
    CALL WAM2NEMO_FIELD%INIT(NEMOUSTOKES=WAM2NEMO%NEMOUSTOKES, NEMOVSTOKES=WAM2NEMO%NEMOVSTOKES, NEMOSTRN=WAM2NEMO%NEMOSTRN, &
&                            NPHIEPS=WAM2NEMO%NPHIEPS, NTAUOC=WAM2NEMO%NTAUOC, NSWH=WAM2NEMO%NSWH, NMWP=WAM2NEMO%NMWP, &
&                            NEMOTAUX=WAM2NEMO%NEMOTAUX, NEMOTAUY=WAM2NEMO%NEMOTAUY, NEMOWSWAVE=WAM2NEMO%NEMOWSWAVE, &
&                            NEMOPHIF=WAM2NEMO%NEMOPHIF)
    CALL INTFLDS_FIELD%INIT(WSEMEAN=INTFLDS%WSEMEAN, WSFMEAN=INTFLDS%WSFMEAN, USTOKES=INTFLDS%USTOKES, VSTOKES=INTFLDS%VSTOKES, &
&                           STRNMS=INTFLDS%STRNMS, TAUXD=INTFLDS%TAUXD, TAUYD=INTFLDS%TAUYD, TAUOCXD=INTFLDS%TAUOCXD, &
&                           TAUOCYD=INTFLDS%TAUOCYD, TAUOC=INTFLDS%TAUOC, PHIOCD=INTFLDS%PHIOCD, PHIEPS=INTFLDS%PHIEPS, &
&                           PHIAW=INTFLDS%PHIAW)
    CALL SRC_CONTRIBS%INIT(FL1=FL1, XLLWS=XLLWS, MIJ=MIJ)

#ifdef WAM_PHYS_GPU
!$acc update device(  &
!$acc & dal2,k11w,lwnemotauoc,satweights,zpi4gm2,omxkm3_gc,rnum,zpifr,dfim_sim,chnkmin_u,z0rat,dthrn_u,ciblock,fklam1,ikp1,sinth,zpi4gm1,x0tauhf,af11,nfre,llgcbz0,bmaxokap,llunstr,flogsprdm1,gm1,lbiwbk,lwfluxout,xkm_gc,idamping,ikm1,cdicwa,dal1,indicessat,ndikcumul,fr,lwnemocousend,egrcrv,lwvflx_snl,sqrtgosurft,cithrsh,cdis,mlsthg,om3gmkm_gc,cofrm4,k1w,isnonlin,afcrv,licerun,cm_gc,fklap,zpi,llnormagam,lwnemocou,rnu,llcapchnk,cithrsh_tail,g,icode_cpl,nwav_gc,dfim,delth,k2w,dfimfr,dfimofr,swellft,ikp,rnlcoef,cdisvis,inlcoef,lwnemocoustrn,tauwshelter,k21w,nfre_odd,fklam,lwcou,lmaskice,lwamrsetci,nsdsnth,mfrstlw,z0tubmax,icode,wtauhf,kfrh,alpha,alphapmax,delkcc_gc_ns,alphamin,gamnconst,dfimfr2,nang,costh,bfcrv,delta_sdis,c2osqrtvg_gc,swellf5,xk_gc,xkmsqrtvgoc2_gc,delkcc_omxkm3_gc,nfre_red,lwflux,ang_gc_c,ikm,omega_gc,rhowg_dfim,cumulw,ang_gc_a,flmax,wspmin,th,tailfactor,idelt,ang_gc_b,dthrn_a,fklap1,lciwabr,iphys,betamaxoxkappa2,zalp,tailfactor_pm,lwnemocoustk,rn1_rn,fr5 &
!$acc &  )
      CALL WVPRPT_FIELD%UPDATE_DEVICE()
      CALL WVENVI_FIELD%UPDATE_DEVICE()
      CALL FF_NOW_FIELD%UPDATE_DEVICE()
      CALL WAM2NEMO_FIELD%UPDATE_DEVICE()
      CALL INTFLDS_FIELD%UPDATE_DEVICE()
      CALL SRC_CONTRIBS%UPDATE_DEVICE()

!$acc data copyin(NPROMA_WAM)
!$acc parallel loop gang vector_length(NPROMA_WAM)
      DO ICHNK=1,NCHNK
        CALL WVPRPT_FIELD%UPDATE_DEVICE_VIEW(ICHNK)
        CALL WVENVI_FIELD%UPDATE_DEVICE_VIEW(ICHNK)
        CALL FF_NOW_FIELD%UPDATE_DEVICE_VIEW(ICHNK)
        CALL WAM2NEMO_FIELD%UPDATE_DEVICE_VIEW(ICHNK)
        CALL INTFLDS_FIELD%UPDATE_DEVICE_VIEW(ICHNK)
        CALL SRC_CONTRIBS%UPDATE_DEVICE_VIEW(ICHNK)
        
        CALL IMPLSCH_SCC(1, NPROMA_WAM, SRC_CONTRIBS%FL1, WVPRPT_FIELD%WAVNUM, WVPRPT_FIELD%CGROUP, WVPRPT_FIELD%CIWA,  &
        & WVPRPT_FIELD%CINV, WVPRPT_FIELD%XK2CG, WVPRPT_FIELD%STOKFAC, WVENVI_FIELD%EMAXDPT, WVENVI_FIELD%INDEP,  &
        & WVENVI_FIELD%DEPTH, WVENVI_FIELD%IOBND, WVENVI_FIELD%IODP, FF_NOW_FIELD%AIRD, FF_NOW_FIELD%WDWAVE,  &
        & FF_NOW_FIELD%CICOVER, FF_NOW_FIELD%WSWAVE, FF_NOW_FIELD%WSTAR, FF_NOW_FIELD%UFRIC, FF_NOW_FIELD%TAUW,  &
        & FF_NOW_FIELD%TAUWDIR, FF_NOW_FIELD%Z0M, FF_NOW_FIELD%Z0B, FF_NOW_FIELD%CHRNCK, FF_NOW_FIELD%CITHICK,  &
        & WAM2NEMO_FIELD%NEMOUSTOKES, WAM2NEMO_FIELD%NEMOVSTOKES, WAM2NEMO_FIELD%NEMOSTRN, WAM2NEMO_FIELD%NPHIEPS,  &
        & WAM2NEMO_FIELD%NTAUOC, WAM2NEMO_FIELD%NSWH, WAM2NEMO_FIELD%NMWP, WAM2NEMO_FIELD%NEMOTAUX, WAM2NEMO_FIELD%NEMOTAUY,  &
        & WAM2NEMO_FIELD%NEMOWSWAVE, WAM2NEMO_FIELD%NEMOPHIF, INTFLDS_FIELD%WSEMEAN, INTFLDS_FIELD%WSFMEAN,  &
        & INTFLDS_FIELD%USTOKES, INTFLDS_FIELD%VSTOKES, INTFLDS_FIELD%STRNMS, INTFLDS_FIELD%TAUXD, INTFLDS_FIELD%TAUYD,  &
        & INTFLDS_FIELD%TAUOCXD, INTFLDS_FIELD%TAUOCYD, INTFLDS_FIELD%TAUOC, INTFLDS_FIELD%PHIOCD, INTFLDS_FIELD%PHIEPS,  &
        & INTFLDS_FIELD%PHIAW, SRC_CONTRIBS%MIJ, SRC_CONTRIBS%XLLWS)
      END DO
!$acc end parallel loop
!$acc end data

      CALL WVPRPT_FIELD%ENSURE_HOST()
      CALL WVENVI_FIELD%ENSURE_HOST()
      CALL FF_NOW_FIELD%ENSURE_HOST()
      CALL WAM2NEMO_FIELD%ENSURE_HOST()
      CALL INTFLDS_FIELD%ENSURE_HOST()
      CALL SRC_CONTRIBS%ENSURE_HOST()
#else
!$OMP  PARALLEL DO SCHEDULE(DYNAMIC,1) PRIVATE(ICHNK) FIRSTPRIVATE(WVPRPT_FIELD, WVENVI_FIELD, FF_NOW_FIELD, WAM2NEMO_FIELD, &
!$OMP& INTFLDS_FIELD, SRC_CONTRIBS)
      DO ICHNK=1,NCHNK
        CALL WVPRPT_FIELD%UPDATE_VIEW(ICHNK)
        CALL WVENVI_FIELD%UPDATE_VIEW(ICHNK)
        CALL FF_NOW_FIELD%UPDATE_VIEW(ICHNK)
        CALL WAM2NEMO_FIELD%UPDATE_VIEW(ICHNK)
        CALL INTFLDS_FIELD%UPDATE_VIEW(ICHNK)
        CALL SRC_CONTRIBS%UPDATE_VIEW(ICHNK)
        
        CALL IMPLSCH(1, NPROMA_WAM, SRC_CONTRIBS%FL1, WVPRPT_FIELD%WAVNUM, WVPRPT_FIELD%CGROUP, WVPRPT_FIELD%CIWA,  &
        & WVPRPT_FIELD%CINV, WVPRPT_FIELD%XK2CG, WVPRPT_FIELD%STOKFAC, WVENVI_FIELD%EMAXDPT, WVENVI_FIELD%INDEP,  &
        & WVENVI_FIELD%DEPTH, WVENVI_FIELD%IOBND, WVENVI_FIELD%IODP, FF_NOW_FIELD%AIRD, FF_NOW_FIELD%WDWAVE,  &
        & FF_NOW_FIELD%CICOVER, FF_NOW_FIELD%WSWAVE, FF_NOW_FIELD%WSTAR, FF_NOW_FIELD%UFRIC, FF_NOW_FIELD%TAUW,  &
        & FF_NOW_FIELD%TAUWDIR, FF_NOW_FIELD%Z0M, FF_NOW_FIELD%Z0B, FF_NOW_FIELD%CHRNCK, FF_NOW_FIELD%CITHICK,  &
        & WAM2NEMO_FIELD%NEMOUSTOKES, WAM2NEMO_FIELD%NEMOVSTOKES, WAM2NEMO_FIELD%NEMOSTRN, WAM2NEMO_FIELD%NPHIEPS,  &
        & WAM2NEMO_FIELD%NTAUOC, WAM2NEMO_FIELD%NSWH, WAM2NEMO_FIELD%NMWP, WAM2NEMO_FIELD%NEMOTAUX, WAM2NEMO_FIELD%NEMOTAUY,  &
        & WAM2NEMO_FIELD%NEMOWSWAVE, WAM2NEMO_FIELD%NEMOPHIF, INTFLDS_FIELD%WSEMEAN, INTFLDS_FIELD%WSFMEAN,  &
        & INTFLDS_FIELD%USTOKES, INTFLDS_FIELD%VSTOKES, INTFLDS_FIELD%STRNMS, INTFLDS_FIELD%TAUXD, INTFLDS_FIELD%TAUYD,  &
        & INTFLDS_FIELD%TAUOCXD, INTFLDS_FIELD%TAUOCYD, INTFLDS_FIELD%TAUOC, INTFLDS_FIELD%PHIOCD, INTFLDS_FIELD%PHIEPS,  &
        & INTFLDS_FIELD%PHIAW, SRC_CONTRIBS%MIJ, SRC_CONTRIBS%XLLWS)
      END DO
!$OMP END PARALLEL DO
#endif
    TIME1(2) = TIME1(2) + (TIME0+WAM_USER_CLOCK())*1.E-06

    IF (LWNEMOCOU) NEMONTAU = NEMONTAU + 1

  ELSE
!   NO SOURCE TERM CONTRIBUTION
!$OMP      PARALLEL DO SCHEDULE(STATIC) PRIVATE(ICHNK)  
    DO ICHNK = 1, NCHNK
      MIJ(:,ICHNK) = NFRE
      FL1(:,:,:,ICHNK) = MAX(FL1(:,:,:,ICHNK), EPSMIN)
      XLLWS(:,:,:,ICHNK) = 0.0_JWRB
    ENDDO
!$OMP      END PARALLEL DO
  ENDIF
  CALL GSTATS(1431,1)


!*       UPDATE FORCING FIELDS TIME COUNTER 
!        ----------------------------------
  IF (LLNEWFILE) THEN
    LLNEWFILE = .FALSE.
    IDELWH = MAX(IDELWI, IDELPRO)
    CALL INCDATE(CDAWIFL, IDELWH)
    CALL INCDATE(CDATEFL, IDELWH)
  ENDIF

  CDATEWO=CDATEWH
  CDTIMP=CDTIMPNEXT
  CALL INCDATE(CDTIMPNEXT, IDELT)   

ENDIF

IF (LHOOK) CALL DR_HOOK('WAMINTGR',1,ZHOOK_HANDLE)

END SUBROUTINE WAMINTGR
